import com.github.blindpirate.gogradle.Go

plugins {
    id("com.github.blindpirate.gogradle") version "0.11.4"
}

golang {
    packagePath = "duty-designator/server"
    goVersion = '1.13.1'
}

dependencies {
    golang {
        build 'name': 'go.mongodb.org/mongo-driver/mongo'
        build 'name': 'github.com/google/uuid'
    }
}

goBuild {
    outputLocation = './.gogradle/server'
}

tasks {
    task("check") {
        dependsOn goCheck
    }

    task("test") {
        dependsOn goTest
    }

    goBuild {
        inputs.dir("routing")
        inputs.dir("test")
        inputs.file("gogradle.lock")
        inputs.file("main.go")
        outputs.file(".gogradle/server")
    }

    task("build") {
        dependsOn goBuild
    }

    task("clean") {
        dependsOn goClean
    }

    task start(type: Exec) {
        dependsOn build, ":client:build"
        commandLine("./.gogradle/server")
    }

    task installErrcheck(type: Go) {
        buildManager.prepareProjectGopathIfNecessary()
        environment PATH: "${System.getenv("PATH")}:$buildManager.goBinaryManager.goroot/bin".toString()
        go 'get github.com/kisielk/errcheck'
    }

    task goErrcheck(type: Go) {
        dependsOn goVendor, installErrcheck
        environment PATH: "${System.getenv("PATH")}:$buildManager.goBinaryManager.goroot/bin".toString()
        go 'run github.com/kisielk/errcheck duty-designator/server/routing duty-designator/server/test'
    }

    goCheck.dependsOn goErrcheck
}